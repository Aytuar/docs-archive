# Tune ActiveMQ logging for large node populations in Puppet Enterprise
<p><strong>Important:</strong> Puppet Enterprise 2018.1 was the last release to support Marionette Collective, also known as MCollective. Future releases of PE do not include MCollective.Â </p>
<p>To prepare for these changes, <a title="Migrate MCollective" href="https://github.com/puppetlabs/docs-archive/blob/main/pe/2018.1/migrating_from_mcollective_to_orchestrator.md">migrate your MCollective work to Puppet orchestrator</a> to automate tasks and create consistent, repeatable administrative processes. Use orchestrator to automate your workflows and take advantage of its integration with Puppet Enterprise console and commands, APIs, role-based access control, and event tracking.</p>
<hr>
<p>The ActiveMQ service limits the amount of log data stored. When you have a large population (thousands) of active MCollective nodes, the limit is exceeded, deleting log data useful for troubleshooting ActiveMQ and MCollective.</p>
<p>Use the following steps to tune ActiveMQ to store 14 days of log data.</p>
<h3 id="version-and-installation-information">Version and installation information</h3>
<p><strong>PE version:</strong> Resolved in PE 2016.4.11, 2017.3.6, and 2018.1.<br>Affects PE versions earlier than 2016.4.11, 2016.5.x, and 2017.1.x to 2017.3.5.</p>
<h3 id="solution">Solution</h3>
<p>On each node running pe-activemq:</p>
<ol style="list-style-type: decimal;">
<li>
<p>Create a manifest named <code>amq_logging_config.pp</code> with the following contents:</p>
<pre>service {'pe-activemq': ensure =&gt; running}

# Configure logrotate to archive and compress files daily.
# 14 days worth of archives will be retained.
file {'/etc/logrotate.d/pe-activemq':
  ensure  =&gt; file,
  owner   =&gt; root,
  mode    =&gt; '0644',
  content =&gt; @(EOF),
    /var/log/puppetlabs/activemq/activemq.log {
      daily
      rotate 14
      dateext

      su pe-activemq pe-activemq
      missingok
      notifempty
      copytruncate
      nocreate
      compress
    }
    | EOF
}

# Configure ActiveMQ to truncate its file if the size exceeds
# 200 MB. This is enough for a couple days of heavy logging
# and serves as a fail-safe in case logrotate is not running.
augeas {'AMQ broker: tune logging':
  incl    =&gt; '/etc/puppetlabs/activemq/log4j.properties',
  lens    =&gt; 'Properties.lns',
  context =&gt; '/files/etc/puppetlabs/activemq/log4j.properties',
  changes =&gt; [
    'set log4j.appender.logfile.maxFileSize "200MB"',
    'set log4j.appender.logfile.maxBackupIndex "0"',
    'set log4j.appender.logfile.append "true"',
    "set log4j.appender.logfile.layout.ConversionPattern \"%d{yyyy-MM-dd'T'HH:mm:ss.SSSXXX} | %-5p | %m | %c | %t%n\"",
  ],
  notify  =&gt; Service['pe-activemq'],
}</pre>
</li>
<li>
<p>Apply the manifest by running <code>puppet apply amq_logging_config.pp</code></p>
</li>
</ol>
